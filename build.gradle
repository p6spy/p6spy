plugins {
    id 'java'
    id 'jacoco'
    id 'maven'
    id 'maven-publish'

    id 'com.fizzpod.sweeney' version '2.1.2'
    id 'com.github.hierynomus.license' version '0.14.0'    
    id 'com.github.johnrengelman.shadow' version '2.0.0'
    // to publish code coverage to coveralls
    // ./gradlew jacocoTestReport coveralls
    id 'com.github.kt3k.coveralls' version '2.8.1'
    // to publish to bintray
    // ./gradlew bintrayUpload
    id 'com.jfrog.bintray' version '1.7.3'
}

def envDB
// sys property (-Denv) or env var (ORG_GRADLE_PROJECT_env) as a fallback
def isTravis = System.getProperty('env', env) != null && System.getProperty('env', env)?.equals('travis')
if (isTravis) {
  // envDB = 'PostgreSQL,MySQL,H2,HSQLDB,SQLite,Firebird,Derby,Oracle,MSSQLServer,MariaDB,Percona'
  envDB = 'PostgreSQL,MySQL,H2,HSQLDB,SQLite,Firebird,Derby,Oracle'
} else {
  // sys property (-DDB) or env var (ORG_GRADLE_PROJECT_DB) as a fallback
  envDB = System.getProperty('DB', DB)
}

repositories {
    mavenCentral()
    flatDir dirs: 'src/test/repo' // holds p6spy-signedjar-test

    if (envDB.contains('Oracle')) {
      assert new File("${buildDir}/repo/ojdbc6.jar").exists()
      flatDir dirs: "${buildDir}/repo/ojdbc6.jar" // holds copied oracle jdbc driver
    }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7
compileJava.options.encoding = 'UTF8'
javadoc.options.encoding = 'UTF8'

dependencies {

  testCompile 'p6spy:p6spy-signedjar-test:1.0.0'

  // for providing misc *-nodep artifacts, please note that order matters here
  // even if slf4j complains about multiple implementations on classpath, it takes the 1.st one
  // and as we use log4j in tests => bridge needs to be the 1.st one specified 
  compileOnly 'org.slf4j:slf4j-api:1.7.7'
  compileOnly 'org.slf4j:slf4j-log4j12:1.7.7'
  compileOnly 'log4j:log4j:1.2.17'
  testCompile 'org.slf4j:slf4j-api:1.7.7'
  testCompile 'org.slf4j:slf4j-log4j12:1.7.7'
  testCompile 'log4j:log4j:1.2.17'

  compileOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.0-rc1'
  compileOnly 'org.apache.logging.log4j:log4j-api:2.0-rc1'
  compileOnly 'org.apache.logging.log4j:log4j-core:2.0-rc1'

  compileOnly 'ch.qos.logback:logback-classic:1.1.2' 
  compileOnly 'ch.qos.logback:logback-core:1.1.2'

  testCompile 'junit:junit:4.11'
  testCompile 'org.eclipse.jetty:jetty-plus:8.1.7.v20120910' // datasource testing through JNDI
  testCompile 'commons-dbcp:commons-dbcp:1.4' // datasource testing
  testCompile 'org.codehaus.btm:btm:2.1.4' // xa datasource testing
  testCompile 'org.apache.geronimo.specs:geronimo-jta_1.1_spec:1.1.1'
  testCompile 'org.liquibase:liquibase-core:3.0.8'
  testCompile 'com.mattbertolini:liquibase-slf4j:1.2.1'
  testCompile 'org.springframework:spring-jdbc:3.2.4.RELEASE'
  testCompile 'org.springframework:spring-context:3.2.4.RELEASE'
  testCompile 'com.j256.simplejmx:simplejmx:1.1'
  testCompile 'commons-io:commons-io:2.4'
  testCompile 'org.apache.commons:commons-exec:1.2'
  testCompile 'org.apache.commons:commons-lang3:3.3.2'
  testCompile 'commons-beanutils:commons-beanutils:1.9.1'
  testCompile 'org.mockito:mockito-core:2.7.22'

  // all the JDBC drivers tested
  testRuntime 'mysql:mysql-connector-java:5.1.21'
  testRuntime 'org.postgresql:postgresql:9.3-1100-jdbc41'
  testCompile 'org.hsqldb:hsqldb:2.3.1'
  testCompile 'com.h2database:h2:1.4.196'
  testRuntime 'org.xerial:sqlite-jdbc:3.7.2' // type 3 driver
  testRuntime 'org.apache.derby:derby:10.10.1.1'
  testRuntime 'org.firebirdsql.jdbc:jaybird-jdk17:2.2.3' // type 4.1 driver
  testRuntime 'com.microsoft.sqlserver:mssql-jdbc:6.1.0.jre7'

  if (envDB.contains('Oracle')) {
    testRuntime name: 'ojdbc6' // oracle copied from docker oracle container
  }
}

sweeney {
    // require Java 1.7 or later for the build
    enforce type: 'range', expect: '[1.7,)', value: {System.getProperty('java.version')}
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

license {
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'P6Spy'

    header file('./script/qa/license_header.txt')
}

test {
    systemProperty 'user.language', 'en'
    systemProperty 'user.country', 'US'
    systemProperty 'derby.stream.error.file', 'target/derby.log'
    systemProperty 'DB', envDB
    
    // to enable remote JMX testing, 
    // see: http://stackoverflow.com/questions/5552960/how-to-connect-to-a-java-program-on-localhost-jvm-using-jmx
    // to workaround oracle timezone issue: http://stackoverflow.com/questions/9156379/ora-01882-timezone-region-not-found
    // to get rid of DB2 traces => -Ddb2.jcc.override.traceLevel=0
    // see: http://publib.boulder.ibm.com/infocenter/idshelp/v111/index.jsp?topic=/com.ibm.jccids.doc/com.ibm.db2.luw.apdv.java.doc/doc/r0052075.htm -->
    jvmArgs '-Dcom.sun.management.jmxremote', '-Dcom.sun.management.jmxremote.authenticate=false', '-Dcom.sun.management.jmxremote.port=1234', '-Dcom.sun.management.jmxremote.ssl=false', '-Doracle.jdbc.timezoneAsRegion=false', '-Ddb2.jcc.override.traceLevel=0'
}

group = 'p6spy'
archivesBaseName = 'p6spy'
version = '3.0.1-SNAPSHOT'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
			groupId project.group
      artifactId project.archivesBaseName
      version project.version

      // https://discuss.gradle.org/t/maven-publish-plugin-generated-pom-making-dependency-scope-runtime/7494/10
      pom.withXml {
          asNode().dependencies.'*'.findAll() {
              it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                  dep.name == it.artifactId.text()
              }
          }.each() {
              it.scope*.value = 'compile'
          }
      }
		}
	}
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['maven']
	pkg {
		repo = 'maven'
		name = 'p6spy'
		userOrg = 'p6spy'
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/p6spy/p6spy.git'
		labels = []
		publicDownloadNumbers = true
		// attributes= ['a': ['ay1', 'ay2'], 'b': ['bee'], c: 'cee'] //Optional package-level attributes
		version {
			name = project.version
			vcsTag = "p6spy-${project.version}"
		}
	}
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

task shadowLog4jJar(type: ShadowJar) {
  classifier = 'log4j-nodep'
  from sourceSets.main.output //tells the task to include the project code
  configurations = [ project.configurations.compileOnly ]
  dependencies {
    include(dependency( group: 'log4j', name: 'log4j')) 
    include(dependency( group: 'org.slf4j', name: 'slf4j-api')) 
    include(dependency( group: 'org.slf4j', name: 'slf4j-log4j')) 
  }
}

task shadowLog4j2Jar(type: ShadowJar) {
  classifier = 'log4j2-nodep'
  from sourceSets.main.output //tells the task to include the project code
  configurations = [ project.configurations.compileOnly ]
  dependencies {
    include(dependency( group: 'org.slf4j', name: 'slf4j-api')) 
    include(dependency( group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl')) 
    include(dependency( group: 'org.apache.logging.log4j', name: 'log4j-api')) 
    include(dependency( group: 'org.apache.logging.log4j', name: 'log4j-core')) 
  }
}

task shadowLogbackJar(type: ShadowJar) {
  classifier = 'logback-nodep'
  from sourceSets.main.output //tells the task to include the project code
  configurations = [ project.configurations.compileOnly ]
  dependencies {
    include(dependency( group: 'org.slf4j', name: 'slf4j-api')) 
    include(dependency( group: 'ch.qos.logback', name: 'logback-classic')) 
    include(dependency( group: 'ch.qos.logback', name: 'logback-core')) 
  }
}

assemble.dependsOn('shadowLog4jJar', 'shadowLog4j2Jar', 'shadowLogbackJar')
